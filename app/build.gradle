import com.caiqueluz.buildsrc.AppConfig

apply plugin: libs.plugins.androidApplication.get().pluginId
apply plugin: libs.plugins.kotlin.android.get().pluginId
apply plugin: libs.plugins.kotlin.kapt.get().pluginId
apply plugin: libs.plugins.koin.get().pluginId

android {
    namespace = AppConfig.APPLICATION_ID
    compileSdk = AppConfig.COMPILE_SDK
    buildToolsVersion = AppConfig.BUILD_TOOLS

    defaultConfig {
        applicationId AppConfig.APPLICATION_ID
        minSdkVersion AppConfig.MIN_SDK
        targetSdkVersion AppConfig.TARGET_SDK
        versionCode AppConfig.VERSION_CODE
        versionName AppConfig.VERSION_NAME

        testInstrumentationRunner AppConfig.TEST_INSTRUMENTATION_RUNNER
    }

    buildTypes {
        all {
            buildConfigField "String", "BASE_URL", COINMARKETCAP_BASE_URL
            buildConfigField "String", "API_KEY", COINMARKETCAP_API_KEY
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = AppConfig.JVM_TARGET
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    sourceSets {
        String sharedDir = 'src/sharedTest/java'
        String sharedResources = 'src/sharedTest/assets'

        test {
            java.srcDir sharedDir
            resources.srcDirs += sharedResources
        }

        androidTest {
            java.srcDir sharedDir
            resources.srcDirs += sharedResources
        }
    }
}

dependencies {
    implementation libs.kotlin.stdLib

    implementation libs.androidx.core.coreKtx
    implementation libs.androidx.appCompat
    implementation libs.google.material
    implementation libs.androidx.constraintLayout
    implementation libs.androidx.archCore.common
    implementation libs.androidx.archCore.runtime
    implementation libs.androidx.fragment.fragmentKtx
    implementation libs.androidx.viewPager2
    implementation libs.androidx.lifecycle.viewModel
    implementation libs.androidx.lifecycle.liveData
    implementation libs.androidx.lifecycle.runtime
    implementation libs.coroutines.android

    implementation libs.coroutines.core
    implementation libs.retrofit
    implementation libs.retrofit.converterGson
    implementation libs.gson
    implementation libs.okHttp
    implementation libs.okHttp.loggingInterceptor
    implementation libs.picasso
    implementation libs.koin.core
    implementation libs.koin.scope
    implementation libs.koin.viewModel
    implementation libs.koin.fragment

    testImplementation libs.junit
    testImplementation libs.androidx.testJUnit
    testImplementation libs.mockito.core
    testImplementation libs.mockito.kotlin
    testImplementation libs.mockito.android
    testImplementation libs.coroutines.test
    testImplementation libs.androidx.archCore.testing

    debugImplementation(libs.androidx.fragment.fragmentTesting) {
        exclude group: 'androidx.test', module: 'core'
    }

    testImplementation libs.robolectric
    testImplementation libs.androidx.testEspresso.core

    androidTestImplementation libs.androidx.testJUnit
    androidTestImplementation libs.androidx.testEspresso.core
    androidTestImplementation libs.mockito.core
    androidTestImplementation libs.mockito.android
    androidTestImplementation libs.mockito.kotlin
    androidTestImplementation libs.androidx.testRunner
    androidTestImplementation libs.coroutines.test
    androidTestImplementation libs.androidx.archCore.testing

    debugImplementation(libs.androidx.fragment.fragmentTesting) {
        exclude group: 'androidx.test', module: 'core'
    }

    androidTestImplementation libs.robolectric
}
